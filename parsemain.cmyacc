name ParseMainFun

terminal IDENT of symbol
terminal NUMBER of int
terminal ARROW
terminal COLON
terminal EQUAL
terminal FOLLOWER
terminal NAME
terminal NONTERMINAL
terminal NOPREC
terminal LPAREN
terminal OF
terminal PRECL
terminal PRECR
terminal RPAREN
terminal START
terminal TERMINAL

nonterminal Ident : symbol =
   1:IDENT => sole_ident

nonterminal Number : int =
   1:NUMBER => sole_number

nonterminal Label : label =
   1:Ident => ident_label
   1:Number => number_label

nonterminal Constituent : constituent =
   1:Ident => unlabeled_item
   1:Label COLON 2:Ident => labeled_item
   LPAREN 1:Constituent RPAREN => paren_item

nonterminal Constituents : constituents =
   => nil_constituents
   1:Constituent 2:Constituents => cons_constituents

nonterminal Precedence : precedence =
   => empty_precedence
   PRECL 1:Number => left_precedence
   PRECR 1:Number => right_precedence
   NOPREC => no_precedence

nonterminal Production : production =
   1:Constituents ARROW 2:Ident 3:Precedence => sole_production

nonterminal Productions : productions =
   => nil_productions
   1:Production 2:Productions => cons_productions

nonterminal Directive : directive =
   NAME 1:Ident => name_directive
   TERMINAL 1:Ident 2:Precedence => terminal_directive
   TERMINAL 1:Ident OF 2:Ident 3:Precedence => terminal_of_directive
   NONTERMINAL 1:Ident COLON 2:Ident EQUAL 3:Productions => nonterminal_directive
   START 1:Ident => start_directive
   FOLLOWER 1:Ident => follower_directive

nonterminal Directives : directives =
   => nil_directives
   1:Directive 2:Directives => cons_directives

start Directives
