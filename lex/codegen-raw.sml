
structure CodegenRaw :> CODEGEN =
   struct

      exception Error

      fun writeProgram filename (options, symbolLimit, types, actions, functions) =
         let
            val outs = TextIO.openOut filename
            fun write str = TextIO.output (outs, str)
         in
            write "#|\nFile generated by CM-Lex version ";
            write Version.version;
            write "\n\n";
            WriteAutomata.writeAutomata outs functions;
            write "\n|#\n\n`(lexer\n    (limit ";
            write (Int.toString symbolLimit);
            write ")";
            app
               (fn (name, _, (count, initial, lastFinalSink, lastFinal, final, trans, transEos)) =>
                   (
                   write "\n    (";
                   write name;
                   write "\n       (size ";
                   write (Int.toString count);
                   write ")\n       (initial ";
                   write (Int.toString initial);
                   write ")\n       (last-final-sink ";
                   write (Int.toString lastFinalSink);
                   write ")\n       (last-final ";
                   write (Int.toString lastFinal);
                   write ")\n       (actions";
                   Array.app (fn str => (write " ,"; write str)) final;
                   write ")\n       (trans";
                   Array.app
                      (fn arr =>
                          (
                          write "\n          (row";
                          Array.app (fn i => (write " "; write (Int.toString i))) arr;
                          write ")"
                          )) trans;
                   write ")\n       (trans-eos\n          (row";
                   Array.app (fn i => (write " "; write (Int.toString i))) transEos;
                   write ")))"
                   )) functions;
            write ")\n";
            TextIO.closeOut outs
         end
                   
   end
